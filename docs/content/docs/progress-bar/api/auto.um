@bugfix 1.1.0
  @issue 78
  @description
    Updated the progressbar background color to add more contrast

@prototype ProgressBarV1
  @deprecated nextReleaseVersion
    @description
      Replaced by @code[ProgressBarV2].

      Pass @code[featureFlags.useUpdatedClass: true] in the constructor options to create a new style progress bar.

  @description
    Create one of these for each progress bar in your page. This object provides methods to get and set the value of the progressbar.

    This prototype is not exposed directly. Use @code[hx.ProgressBar].

  @constructor
    @removed 0.13.0
      @description
        Added options to the constructor

    @description
      Creates a progress bar

    @arg selector [String/HTMLElement]
      @description
        The selector to create the progressbar inside.

  @method setProgress
    @deprecated 0.11.0
      @description
        Use @code[progress(value)] instead.

    @removed 0.12.0
    @description
      Set the value of the progress bar. Expects a value between 0 and 1.

    @arg value [Number]
      @description
        A value between 0 and 1.

  @method getProgress
    @deprecated 0.11.0
      @description
        Use @code[progress()] instead.

    @removed 0.12.0
    @description
      Get the value of the progress bar.

    @returns [Number]
      @description
        A value between 0 and 1

  @method value
    @added 0.11.0
    @description
      Set the value of the progress bar. Expects a value between 0 and 1.

    @arg value [Number]
      @description
        A value between 0 and 1.

    @returns [ProgressBar]
      @description
        This progressbar for chaining.

  @method value
    @added 0.11.0
    @description
      Get the value of the progress bar.

    @returns [Number]
      @description
        A value between 0 and 1

  @method segments
    @added 0.12.5
      @description
        Added a way to create multi-segment progress bars

    @description
      A method for applying an array of segments to a progress bar.

    @arg segments [Array]
      @updated 0.13.0
        @description
          Added the ability to clear the segments by passing in undefined as the segments parameter.

      @description
        The array of segments to apply. It should be in the format of an array of objects with a value or ratio property and an optional class property.

        Every segment must have either a value or a ratio and every item in the array of segments must have the same property, values and ratios can not be used in conjunction with one another.

        If only a class is provided for each segment, the progressbar will use a default ratio of 1:1 for every segment.

        @@codeblock js
          [
            { // Segment would be 25% of the total size of the progress bar and positive
              "class": "hx-positive"
              "value": 0.25
            },
            { // Segment would be 75% of the total size of the progress bar
              "value": 0.75
            }
          ]

        The below segments array would give one negative segment of 60% width, one warning segment of 20% width and one positive segment of 20% width.

        @@codeblock js
          [
            {
              "class": "hx-negative"
              "ratio": 3
            }
            {
              "class": "hx-warning"
            }
            {
              "class": "hx-positive"
              "ratio": 1
            }
          ]

        Passing in undefined as the segments parameter clears the segments and sets the progressbar to a single bar. The retainProgress parameter can be used in conjuntion with this to retain the current progress.

    @arg? retainProgress [Boolean]
      @description
        Whether to retain the current progress of the progressbar.

      @default
        false

    @returns [ProgressBar]
      @description
        This progressbar for chaining.

  @method segments
    @added 0.12.5
    @description
      A method for getting the segments in a progressbar

    @returns [Array]
      @description
        The currently set segments for the progressbar

  @constructor
    @added 0.13.0
      @description
        Added options to the constructor

    @description
      Creates a progress bar

    @arg selector [String/HTMLElement]
      @description
        The selector to create the progressbar inside.

    @arg? options [Object]
      @description
        The options to use when setting up the progressbar.

      @property segments [Array]
        @description
          The array of segments to initialise the progressbar with.

      @property value [Number]
        @description
          The value of the progressbar when it is initialised

        @default
          0

      @property animate [Boolean]
        @description
          Whether to animate the progress changes for the progressbar by adding the hx-animate class.

        @default
          false

@prototype ProgressBarV2
  @added nextReleaseVersion

  @description
    This prototype is not exposed directly. Use @code[hx.ProgressBar] with the @code[featureFlags.useUpdatedClass: true] option.

  @constructor
    @arg selector [String/HTMLElement/Selection]
      @description
        The selector to create the progress bar inside.

    @arg? options [Object]
      @property title [String]
        @description
          Sets the title to display above the progress bar
        @default: @code[undefined]

      @property breakdown [String]
        @description
          Sets the 'breakdown text' to display below the progress bar.
          This is intended to explain what the progress bar and its segments are showing.

        @default: @code[undefined]

      @property plan [Number]
        @description
          Sets the plan for the progress bar. This is the target number used to indicate the total progress.

          It is shown above and to the right of the progress bar like @code[progress / plan] (e.g. @code[100 / 1200])

        @default: @code[0]

      @property inProgress [Number]
        @description
          The current number of items that are being actioned but have not been completed.

        @default: @code[0]

      @property done [Number]
        @description
          The number of completed items.

          This number will be shown next to the plan. It is shown above and to the right of
          the progress bar like @code[progress / plan] (e.g. @code[100 / 1200])

          The @code[done] amount can be greater than the planned amount (e.g. when you have completed more than you planned to complete)

        @default: @code[0]

      @property hidePlan [Boolean]
        @description
          Whether to hide the plan and just show the progress bar with title/breakdown text.

        @default: @code[false]

      @property compact [Boolean]
        @description
          Whether the progress bar should be compact. In this mode, the bar will be shorter and not display numbers on the segments.

        @default: @code[false]

      @property disabled [Boolean]
        @description
          Whether the progress bar is disabled. This can be used when there is no data or when a bar is not applicable.
        @default: @code[false]

  @method title
    @description
      Gets the title option
    @returns [String]

  @method title
    @description
      Sets the title option

    @arg title [String]

    @returns ProgressBar
      @description
        This ProgressBar

  @method breakdown
    @description
      Gets the breakdown option
    @returns [String]

  @method breakdown
    @description
      Sets the breakdown option

    @arg breakdown [String]

    @returns ProgressBar
      @description
        This ProgressBar

  @method plan
    @description
      Gets the plan option
    @returns [Number]

  @method plan
    @description
      Sets the plan option

    @arg plan [Number]

    @returns ProgressBar
      @description
        This ProgressBar

  @method inProgress
    @description
      Gets the inProgress option

    @returns [Number]

  @method inProgress
    @description
      Sets the inProgress option

    @arg inProgress [Number]

    @returns ProgressBar
      @description
        This ProgressBar

  @method done
    @description
      Gets the done option

    @returns [Number]

  @method done
    @description
      Sets the done option

    @arg done [Number]

    @returns ProgressBar
      @description
        This ProgressBar

  @method hidePlan
    @description
      Gets the hidePlan option

    @returns [Boolean]

  @method hidePlan
    @description
      Sets the hidePlan option

    @arg hidePlan [Boolean]

    @returns ProgressBar
      @description
        This ProgressBar

  @method compact
    @description
      Gets the compact option

    @returns [Boolean]

  @method compact
    @description
      Sets the compact option

    @arg compact [Boolean]

    @returns ProgressBar
      @description
        This ProgressBar

  @method disabled
    @description
      Gets the disabled option

    @returns [Boolean]

  @method disabled
    @description
      Sets the disabled option

    @arg disabled [Boolean]

    @returns ProgressBar
      @description
        This ProgressBar

@prototype hx.ProgressBar
  @description
    Creates a Progress Bar element.

  @constructor
    @collapsed false
    @arg selector [String/HTMLElement/Selection]
      @description
        The selection to create the progress bar inside.

    @arg? options [Object]
      @collapsed false
      @description
        The options to use when creating the progress bar.

        There are two classes used internally, @code[ProgressBarV1] and @code[ProgressBarV2].

        By default, this will create a @code[ProgressBarV1]. If passing
        @code[featureFlags.useUpdatedClass: true] in the options, a @code[ProgressBarV2] will be created.

        The other available options differ for each of the two prototypes. See their constructors for details.

      @property featureFlags [Object]
        @added nextReleaseVersion
          @description
            Added featureFlags to enable a new progress bar class
        @collapsed false
        @property useUpdatedClass [Boolean]
          @collapsed false
          @description
            Whether to use the ProgressBarV2 prototype instead of ProgressBarV1

@class hx-progressbar
  @deprecated 0.9.0
    @description
      Use hx-progress-bar instead.

  @removed 0.10.0
    @description
      Use hx-progress-bar instead.

  @description
    The class applied to the selector.

@class hx-progress-bar
  @added 0.9.0
  @updated 0.10.0
    @description
      Removed module-specific classes in favor of generic classes

  @bugfix 1.0.0
    @description
      Updated the progress bar styles to remove the need for @code[!important] flags.

  @description
    The class given to the root element of a progress bar

  @extraClass hx-positive
    @description
      Adds progress bar styling indicative of success, confirmation or something positive.

  @extraClass hx-negative
    @description
      Adds progress bar styling indicative of failure, danger or something negative.

  @extraClass hx-warning
    @description
      Adds progress bar styling indicative of warning.

  @extraClass hx-compliment
    @removed 1.0.0
      @description
        Use @code[.hx-complement] instead

    @description
      Styles the progress bar with a light color on a light theme, and a dark color on a dark theme.

  @extraClass hx-contrast
    @description
      Styles the progress bar with a dark color on a light theme, and a light color on a dark theme.

  @extraClass hx-info
    @description
      Styles the progress bar to indicate that clicking it will lead to some form of documentation / information.

  @extraClass [hx-progressbar-[n]]
    @deprecated 0.9.0
      @description
        All numbered color classes will be removed in a later version.Use the context classes instead.

    @removed 0.10.0

  @extraClass hx-animate
    @added 0.12.5
      @description
        Added a class to animate the changes in progress of the progress bar to give a smooth motion.

    @description
      A class that changes the progressbar to use CSS animations when changing the progress to give smooth motion.

  @extraClass hx-action
    @added 1.0.0
      @description
        Added new action context class and updated default styles.

    @description
      Styles the progress bar with action colors

  @extraClass hx-complement
    @description
      Styles the progress bar with complement colors

@function hx.progressBar
  @added 1.0.0
    @description
      Fluid api version of ProgressBar

  @description
    Creates a new ProgressBar set up on a detached element, wrapped in a selection

  @arg? options [Object]
    @collapsed false
    @description
      See the options object for constructing ProgressBar.

      By default, this will create a @code[ProgressBarV1]. If passing
      @code[featureFlags.useUpdatedClass: true] in the options, a @code[ProgressBarV2] will be created.

      The other available options differ for each of the two prototypes. See their constructors for details.

    @property featureFlags [Object]
      @added nextReleaseVersion
        @description
          Added featureFlags to enable a new progress bar class
      @collapsed false
      @property useUpdatedClass [Boolean]
        @collapsed false
        @description
          Whether to use the ProgressBarV2 prototype instead of ProgressBarV1

  @returns [Selection]
    @description
      A selection containing an element with an ProgressBar initialised on it

